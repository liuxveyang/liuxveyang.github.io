<!DOCTYPE html>
<html lang="zh-cn">
	<head>
    <meta charset="utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="author" content="liuxueyang">
<meta name="description" content="舊博客">
<meta name="generator" content="Hugo 0.48" />
<title>Text Adventure Game in Land of Lisp</title>
<link rel="shortcut icon" href="https://liuxveyang.github.io/images/favicon.ico">
<link rel="stylesheet" href="https://liuxveyang.github.io/css/style.css">
<link rel="stylesheet" href="https://liuxveyang.github.io/css/highlight.css">



<link rel="stylesheet" href="https://liuxveyang.github.io/css/monosocialiconsfont.css">



<link href="https://liuxveyang.github.io/index.xml" rel="alternate" type="application/rss+xml" title="LXY Site" />


<meta property="og:title" content="Text Adventure Game in Land of Lisp" />
<meta property="og:description" content="Land of LiSP 这本书里面的第五章和第六章构建了一个字符冒险游戏，游戏截图如下：
;; 2016.07.30 23:22:41 PM ;; Sabastian ;; building a text adventure game engine ;; ==================== ;; location (defparameter *nodes* &#39;((living-room (you are in the living-room." />
<meta property="og:type" content="article" />
<meta property="og:url" content="https://liuxveyang.github.io/posts/text-adventure-game-in-land-of-lisp/" /><meta property="article:published_time" content="2016-08-01T23:19:46&#43;00:00"/>
<meta property="article:modified_time" content="2016-08-01T23:19:46&#43;00:00"/>


<meta itemprop="name" content="Text Adventure Game in Land of Lisp">
<meta itemprop="description" content="Land of LiSP 这本书里面的第五章和第六章构建了一个字符冒险游戏，游戏截图如下：
;; 2016.07.30 23:22:41 PM ;; Sabastian ;; building a text adventure game engine ;; ==================== ;; location (defparameter *nodes* &#39;((living-room (you are in the living-room.">


<meta itemprop="datePublished" content="2016-08-01T23:19:46&#43;00:00" />
<meta itemprop="dateModified" content="2016-08-01T23:19:46&#43;00:00" />
<meta itemprop="wordCount" content="831">



<meta itemprop="keywords" content="Common Lisp," />

<meta name="twitter:card" content="summary"/>
<meta name="twitter:title" content="Text Adventure Game in Land of Lisp"/>
<meta name="twitter:description" content="Land of LiSP 这本书里面的第五章和第六章构建了一个字符冒险游戏，游戏截图如下：
;; 2016.07.30 23:22:41 PM ;; Sabastian ;; building a text adventure game engine ;; ==================== ;; location (defparameter *nodes* &#39;((living-room (you are in the living-room."/>


    </head>
<body>
    <nav class="main-nav">
	
		<a href='https://liuxveyang.github.io'> <span class="arrow">←</span>Home</a>
	

	

	
		<a class="cta" href="https://liuxveyang.github.io/index.xml">Subscribe</a>
	
</nav>

    <section id="wrapper">
        
        
<article class="post">
    <header>
        <h1>Text Adventure Game in Land of Lisp</h1>
        <h2 class="subtitle"></h2>
        <h2 class="headline">
        August 1, 2016
        <br>
        
        
            
                <a href="https://liuxveyang.github.io/tags/common-lisp">Common Lisp</a>
            
        
        
        </h2>
    </header>
    <section id="post-body">
        <p><strong>Land of LiSP</strong> 这本书里面的第五章和第六章构建了一个字符冒险游戏，游戏截图如下：</p>

<p><img src="/img/text-adventure-game.png" alt="Text Adventure Game" /></p>

<pre><code class="language-lisp">;; 2016.07.30 23:22:41 PM
;; Sabastian
;; building a text adventure game engine

;; ====================
;; location

(defparameter *nodes*
  '((living-room (you are in the living-room.
                  a wizard is snoring loudly on the couch.))
    (garden (you are in a beautiful garden.
             there is a well in front of you.))
    (attic (you are in the attic.
            there is a giant welding torch in the corner.))))

(defun describe-location (location nodes)
  &quot;location is a symbol.
nodes is the global variable.&quot;
  (cadr (assoc location nodes)))

;; ====================
;; edges

(defparameter *edges*
  '((living-room
     (garden west door)
     (attic upstairs ladder))
    (garden
     (living-room east door))
    (attic
     (living-room downstairs ladder))))

(defun describe-path (edge)
  `(there is a ,(caddr edge) going ,(cadr edge) from here.))

(defun describe-paths (location edges)
  &quot;location is a symbol.
edges is the global variable&quot;
  (apply #'append
         (mapcar #'describe-path
                 (cdr (assoc location edges)))))

;; ====================
;; objects

(defparameter *objects* '(whiskey bucket frog chain))
(defparameter *object-locations* '((whiskey living-room)
                                   (bucket living-room)
                                   (chain garden)
                                   (frog garden)))


;; One Way to write OBJECTS-AT function

;; (defun object-at-location-p (object location object-locations)
;;   &quot;object and location are symbols.
;; object-locations is a global variable&quot;
;;   (eq (cadr (assoc object object-locations))
;;       location))

;; if the test function in REMOVE-IF-NOT require more than one
;; arguments, then use a lambda to wrap the function. ;-)

;; (defun objects-at (location objects object-locations)
;;   (remove-if-not #'(lambda (object)
;;                      (object-at-location-p object
;;                                            location
;;                                            object-locations))
;;                  objects))

;; A better way for OBJECTS-AT function
(defun objects-at (location objects object-locations)
  &quot;location is a symbol.
objects and object-locations are global variables.
return a list of objects in the current location.&quot;
  (labels ((at-location-p (object)
             (eq location
                 (cadr (assoc object object-locations)))))
    (remove-if-not #'at-location-p objects)))

(defun describe-objects (location objects object-locations)
  &quot;location is a symbol.
objects and object-locations are global variables.&quot;
  (apply #'append
         (mapcar #'(lambda (object)
                     `(you see a ,object on the floor.))
                 (objects-at location objects
                             object-locations))))

;; ====================
;; Describing it ALL

(defparameter *location* 'living-room)

(defun look ()
  (append (describe-location *location* *nodes*)
          (describe-paths *location* *edges*)
          (describe-objects *location* *objects*
                            *object-locations*)))

;; Walking Around in Our World
(defun walk (direction)
  (let ((next
          (find direction
                (cdr (assoc *location* *edges*))
                :key #'cadr)))
    (cond (next
           (setf *location* (car next))
           (look))
          (t
           '(you cannot go that way.)))))

;; ====================
;; Pick Up Objects

(defun pickup (object)
  &quot;object is a symbol.
push a new list to *object-locations*, return a message list&quot;
  (cond ((member
          object
          (objects-at *location* *objects*
                      *object-locations*))
         (push `(,object body) *object-locations*)
         `(you are now carrying the ,object))
        (t '(you cannot get that.))))

;; ====================
;; Checking Our Inventory

(defun inventory ()
  (cons 'items- (objects-at 'body *objects* *object-locations*)))

(defun game-read ()
  (let ((cmd
          (read-from-string
           (concatenate 'string &quot;(&quot; (read-line) &quot;)&quot;))))
    (cons (car cmd)
          (mapcar #'(lambda (x)
                      `',x)
                  (cdr cmd)))))

(defun tweak-text (lst capital-p literal-p)
  (let ((item (car lst))
        (rest (rest lst)))
    (if item
        (cond ((member item '(#\! #\? #\.))
               (cons item (tweak-text rest t nil)))
              ((eq item #\&quot;)
               (cons item (tweak-text rest nil (not literal-p))))
              (literal-p
               (cons item (tweak-text rest nil literal-p)))
              (capital-p
               (cons (char-upcase item)
                     (if (alpha-char-p item)
                         (tweak-text rest nil literal-p)
                         (tweak-text rest t literal-p))))
              (t
               (cons (char-downcase item)
                     (tweak-text rest nil literal-p)))))))

(defun game-print (lst)
  (princ
   (coerce
    (tweak-text
     (coerce (string-trim &quot;() &quot;
                          (prin1-to-string lst))
             'list)
     t nil)
    'string))
  (fresh-line))

(defparameter *allowed-commands* '(look walk pickup inventory))

(defun game-eval (sexp)
  (if (member (first sexp)
              *allowed-commands*)
      (eval sexp)
      '(i do not know that command.)))

(defun game-repl ()
  (let ((cmd (game-read)))
    (unless (eq (car cmd)
                'quit)
      (game-print (game-eval cmd))
      (game-repl))))

</code></pre>

<p>在第七章里，把游戏里面的位置位置关系通过有向图的形式用第三方库画出来：</p>

<p><img src="/img/wizard.png" alt="adventure game world" /></p>

<pre><code class="language-lisp">;; 2016/08/01 11:48:58 AM
;; Xueyang Liu

(defparameter *wizard-nodes*
  '((living-room (you are in the living-room.
                  a wizard is snoring loudly on the couch.))
    (garden (you are in a beautiful garden.
             there is a well in front of you.))
    (attic (you are in the attic.
            there is a giant welding torch in the corner.))))

(defparameter *wizard-edges*
  '((living-room
     (garden west door)
     (attic upstairs ladder))
    (garden
     (living-room east door))
    (attic
     (living-room downstairs ladder))))

(defun dot-name (sexp)
  (string-downcase
   (substitute-if #\_ (complement #'alphanumericp)
                  (prin1-to-string sexp))))

(defparameter *max-label-length* 30)

(defun dot-label (sexp)
  (if sexp
      (let ((s (write-to-string sexp
                                :pretty nil)))
        (if (&gt; (length s)
               *max-label-length*)
            (concatenate 'string
                         (string-downcase
                          (subseq s
                                  0 (- *max-label-length* 3)))
                         &quot;...&quot;)
            s))
      &quot;&quot;))

(defun nodes-&gt;dot (nodes)
  (mapc (lambda (sexp)
          (fresh-line)
          (princ (dot-name (car sexp)))
          (princ &quot;[label=\&quot;&quot;)
          (princ (dot-label sexp))
          (princ &quot;\&quot;];&quot;))
        nodes))

(defun edges-&gt;dot (edges)
  (mapc (lambda (node)
          (mapc (lambda (edge)
                  (fresh-line)
                  (princ (dot-name (car node)))
                  (princ &quot;-&gt;&quot;)
                  (princ (dot-name (car edge)))
                  (princ &quot;[label=\&quot;&quot;)
                  (princ (string-downcase
                          (prin1-to-string (cdr edge))))
                  (princ &quot;\&quot;];&quot;))
                (cdr node)))
        edges))

(defun uedges-&gt;dot (edges)
  (maplist (lambda (lst)
             (mapc (lambda (edge)
                     (unless (assoc (car edge)
                                    lst)
                       (fresh-line)
                       (princ (dot-name (caar lst)))
                       (princ &quot;--&quot;)
                       (princ (dot-name (car edge)))
                       (princ &quot;[label=\&quot;&quot;)
                       (princ (string-downcase
                               (prin1-to-string (cdr edge))))
                       (princ &quot;\&quot;];&quot;)))
                   (cdar lst)))
           edges))

(defun graph-&gt;dot (nodes edges)
  (princ &quot;digraph{&quot;)
  (nodes-&gt;dot nodes)
  (edges-&gt;dot edges)
  (princ &quot;}&quot;))

(defun ugraph-&gt;dot (nodes edges)
  (princ &quot;graph{&quot;)
  (nodes-&gt;dot nodes)
  (uedges-&gt;dot edges)
  (princ &quot;}&quot;))

(defun dot-&gt;png (fname thunk)
  (with-open-file (*standard-output*
                   fname
                   :direction :output
                   :if-exists :supersede)
    (funcall thunk))
  (ext:shell (concatenate 'string
                          &quot;dot -Tpng -O &quot; fname)))

(defun graph-&gt;png (fname nodes edges)
  (dot-&gt;png fname
            (lambda ()
              (graph-&gt;dot nodes edges))))

(defun ugraph-&gt;png (fname nodes edges)
  (dot-&gt;png fname
            (lambda ()
              (ugraph-&gt;dot nodes edges))))

</code></pre>

    </section>
</article>

<footer id="post-meta" class="clearfix">
    
    <img class="avatar" src="https://liuxveyang.github.io/images/avatar.png">
    <div>
        <span class="dark">liuxueyang</span>
        <span>I am a learner. :P</span>
    </div>
    
    <section id="sharing">
        <a class="twitter" href="https://twitter.com/intent/tweet?text=https%3a%2f%2fliuxveyang.github.io%2fposts%2ftext-adventure-game-in-land-of-lisp%2f - Text%20Adventure%20Game%20in%20Land%20of%20Lisp "><span class="icon-twitter"> tweet</span></a>

<a class="facebook" href="#" onclick="
    window.open(
      'https://www.facebook.com/sharer/sharer.php?u='+encodeURIComponent(location.href),
      'facebook-share-dialog',
      'width=626,height=436');
    return false;"><span class="icon-facebook-rect"> Share</span>
</a>

    </section>
</footer>

<div id="disqus_thread"></div>
<script type="application/javascript">
    var disqus_config = function () {
    
    
    
    };
    (function() {
        if (["localhost", "127.0.0.1"].indexOf(window.location.hostname) != -1) {
            document.getElementById('disqus_thread').innerHTML = 'Disqus comments not available by default when the website is previewed locally.';
            return;
        }
        var d = document, s = d.createElement('script'); s.async = true;
        s.src = '//' + "liuxveyang" + '.disqus.com/embed.js';
        s.setAttribute('data-timestamp', +new Date());
        (d.head || d.body).appendChild(s);
    })();
</script>
<noscript>Please enable JavaScript to view the <a href="https://disqus.com/?ref_noscript">comments powered by Disqus.</a></noscript>
<a href="https://disqus.com" class="dsq-brlink">comments powered by <span class="logo-disqus">Disqus</span></a>

<ul id="post-list" class="archive readmore">
    <h3>Read more</h3>

    
    
    
        <li>
            <a href="/posts/%E8%AF%BB%E4%B9%A6%E7%AC%94%E8%AE%B06%E6%8A%80%E6%9C%AF%E7%B1%BB/">读书笔记6「技术类」<aside class="dates">May 14 2018</aside></a>
        </li>
    
        <li>
            <a href="/posts/%E8%AF%BB%E4%B9%A6%E7%AC%94%E8%AE%B05/">读书笔记5<aside class="dates">Apr 29 2018</aside></a>
        </li>
    
        <li>
            <a href="/posts/%E6%95%B4%E7%90%86%E5%8D%9A%E5%AE%A2/">整理博客<aside class="dates">Apr 29 2018</aside></a>
        </li>
    
        <li>
            <a href="/posts/2017-12-23-summary-for-2017/">2017 年结束了<aside class="dates">Dec 23 2017</aside></a>
        </li>
    
        <li>
            <a href="/posts/clustering/">Clustering<aside class="dates">May 17 2017</aside></a>
        </li>
    
        <li>
            <a href="/posts/building-roads-to-connect-cities/">Building Roads to Connect Cities<aside class="dates">May 17 2017</aside></a>
        </li>
    
        <li>
            <a href="/posts/detecting-anomalies-in-currency-exchange-rates/">Detecting Anomalies in Currency Exchange Rates<aside class="dates">May 17 2017</aside></a>
        </li>
    
        <li>
            <a href="/posts/computing-minimum-cost-of-a-flight/">Computing the Minimum Cost of a Flight<aside class="dates">May 17 2017</aside></a>
        </li>
    
        <li>
            <a href="/posts/checking-whether-a-graph-is-bipartite/">Checking whether a Graph is Bipartite<aside class="dates">May 17 2017</aside></a>
        </li>
    
        <li>
            <a href="/posts/computing-minimum-number-of-flight-segments/">Computing the Minimum Number of Flight Segments<aside class="dates">May 16 2017</aside></a>
        </li>
    
</ul>



        <footer id="footer">
    
        <div id="social">

	
	
    
    <a class="symbol" href="https://www.github.com/liuxueyang">
        circlegithub
    </a>
    


</div>

    
    <p class="small">
    
        © Copyright 2018 liuxueyang
    
    </p>
</footer>

    </section>
    <script src="//ajax.googleapis.com/ajax/libs/jquery/2.1.1/jquery.min.js"></script>
<script src="https://liuxveyang.github.io/js/main.js"></script>
<script src="https://liuxveyang.github.io/js/highlight.js"></script>
<script>hljs.initHighlightingOnLoad();</script>




<script type="application/javascript">
var doNotTrack = false;
if (!doNotTrack) {
	(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
	(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
	m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
	})(window,document,'script','https://www.google-analytics.com/analytics.js','ga');
	ga('create', 'UA-125224924-1', 'auto');
	
	ga('send', 'pageview');
}
</script>


</body>
</html>
